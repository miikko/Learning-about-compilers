/*
 *  cool.cup
 *              Parser definition for the COOL language.
 *
 */

import java_cup.runtime.*;

/* Stuff enclosed in {: :} is copied verbatim to the class containing
all parser actions.  All the extra variables/functions you want to use
in the semantic actions should go here.  Don't remove or modify anything
that was there initially.  */

action code {: 

    int curr_lineno() {
	return ((CoolTokenLexer)parser.getScanner()).curr_lineno();
    }

    AbstractSymbol curr_filename() {
	return ((CoolTokenLexer)parser.getScanner()).curr_filename();
    }
:} 

/************************************************************************/
/*                DONT CHANGE ANYTHING IN THIS SECTION                  */

parser code {:
    int omerrs = 0;

    public void syntax_error(Symbol cur_token) {
        int lineno = action_obj.curr_lineno();
	String filename = action_obj.curr_filename().getString();
        System.err.print("\"" + filename + "\", line " + lineno + 
		         ": parse error at or near ");
        Utilities.printToken(cur_token);
	omerrs++;
	if (omerrs>50) {
	   System.err.println("More than 50 errors");
	   System.exit(1);
	}
    }

    public void unrecovered_syntax_error(Symbol cur_token) {
    }
:}

/* Declare the terminals; a few have types for associated lexemes.  The
token ERROR is never used in the parser; thus, it is a parse error when
the lexer returns it.  */

terminal CLASS, ELSE, FI, IF, IN, INHERITS, LET, LET_STMT, LOOP, POOL, THEN, WHILE;
terminal CASE, ESAC, OF, DARROW, NEW, ISVOID;
terminal ASSIGN, NOT, LE, ERROR;
terminal PLUS, DIV, MINUS, MULT, EQ, LT, DOT, NEG, COMMA, SEMI, COLON;
terminal LPAREN, RPAREN, AT, LBRACE, RBRACE;
terminal AbstractSymbol STR_CONST, INT_CONST;
terminal Boolean BOOL_CONST;
terminal AbstractSymbol TYPEID, OBJECTID;

/*  DON'T CHANGE ANYTHING ABOVE THIS LINE, OR YOUR PARSER WONT WORK       */
/**************************************************************************/

   /* Complete the nonterminal list below, giving a type for the semantic
      value of each non terminal. (See the CUP documentation for details. */

nonterminal programc program;
nonterminal Classes class_list;
nonterminal class_c class;
nonterminal Features feature_list;
nonterminal Feature feature;
nonterminal Formals formal_list;
nonterminal formalc formal;
nonterminal Expressions expr_list_semi, expr_list_comma;
nonterminal Expression expr, disp, let_expr, let_assign;
nonterminal Cases cases;
nonterminal Case case;

/* Precedence declarations go here. */
precedence right ASSIGN;
precedence left NOT;
precedence nonassoc LE, LT, EQ;
precedence left PLUS, MINUS;
precedence left MULT, DIV;
precedence left ISVOID;
precedence left NEG;
precedence left AT;
precedence left DOT;


program	
	::= class_list:cl
	    {: RESULT = new programc(curr_lineno(), cl); :}
        ;

class_list
	/* single class */
	::= class:c
	    {: RESULT = (new Classes(curr_lineno())).appendElement(c); :}
	/* several classes */
	| class_list:cl class:c
	    {: RESULT = cl.appendElement(c); :}
	;

/* If no parent is specified, the class inherits from the Object class */
class
	::= CLASS TYPEID:tid LBRACE feature_list:fl RBRACE SEMI
	    {: RESULT = new class_c(curr_lineno(), tid, 
		                   AbstractTable.idtable.addString("Object"), 
				   fl, curr_filename()); :}
	| CLASS TYPEID:tid INHERITS TYPEID:tid2 LBRACE feature_list:fl RBRACE SEMI
	    {: RESULT = new class_c(curr_lineno(), tid, tid2, fl, curr_filename()); :}
	| error SEMI
	;

/* Feature list may be empty, but no empty features in list. */
feature_list
	::= /* empty */
	    {: RESULT = new Features(curr_lineno()); :}
	| feature_list:fl feature:f
		{: RESULT = fl.appendElement(f); :}
	;

feature
	::= OBJECTID:oid LPAREN formal_list:fl RPAREN COLON TYPEID:tid LBRACE expr:e RBRACE SEMI
		{: RESULT = new method(curr_lineno(), oid, fl, tid, e); :}	
	| OBJECTID:oid COLON TYPEID:tid ASSIGN expr:e SEMI
		{: RESULT = new attr(curr_lineno(), oid, tid, e); :}
	| OBJECTID:oid COLON TYPEID:tid SEMI
		{: RESULT = new attr(curr_lineno(), oid, tid, (new no_expr(curr_lineno()))); :}
	| error SEMI
	;

formal_list
	::= /* empty */
		{: RESULT = new Formals(curr_lineno()); :}
	| formal:f
		{: RESULT = (new Formals(curr_lineno())).appendElement(f); :}
	| formal_list:fl COMMA formal:f
		{: RESULT = fl.appendElement(f); :}
	;

formal
	::= OBJECTID:oid COLON TYPEID:tid
		{: RESULT = new formalc(curr_lineno(), oid, tid); :}
	;

expr_list_semi
	::= expr:e SEMI
		{: RESULT = (new Expressions(curr_lineno())).appendElement(e); :}
	| expr_list_semi:el expr:e SEMI
		{: RESULT = el.appendElement(e); :}
	| error SEMI expr_list_semi
	;

expr_list_comma
	::= /* empty */ 
		{: RESULT = new Expressions(curr_lineno()); :}
	| expr:e
		{: RESULT = (new Expressions(curr_lineno())).appendElement(e); :}
	| expr_list_comma:el COMMA expr:e 
		{: RESULT = el.appendElement(e); :}
	;

expr
	::= OBJECTID:oid ASSIGN expr:e
		{: RESULT = new assign(curr_lineno(), oid, e); :}
	| expr:e AT TYPEID:tid DOT OBJECTID:oid LPAREN expr_list_comma:el RPAREN
		{: RESULT = new static_dispatch(curr_lineno(), e, tid, oid, el); :}
	| disp:di
		{: RESULT = di; :}
	| IF expr:e1 THEN expr:e2 ELSE expr:e3 FI
		{: RESULT = new cond(curr_lineno(), e1, e2, e3); :}
	| WHILE expr:e1 LOOP expr:e2 POOL
		{: RESULT = new loop(curr_lineno(), e1, e2); :}
	| LBRACE expr_list_semi:el RBRACE
		{: RESULT = new block(curr_lineno(), el); :}
	| LET let_expr:le
		{: RESULT = le; :}
	| CASE expr:e1 OF cases:cs ESAC
		{: RESULT = new typcase(curr_lineno(), e1, cs); :}
	| NEW TYPEID:tid
		{: RESULT = new new_(curr_lineno(), tid); :}
	| ISVOID expr:e
		{: RESULT = new isvoid(curr_lineno(), e); :}
	| expr:e1 PLUS expr:e2
		{: RESULT = new plus(curr_lineno(), e1, e2); :}
	| expr:e1 MINUS expr:e2
		{: RESULT = new sub(curr_lineno(), e1, e2); :}
	| expr:e1 MULT expr:e2
		{: RESULT = new mul(curr_lineno(), e1, e2); :}
	| expr:e1 DIV expr:e2
		{: RESULT = new divide(curr_lineno(), e1, e2); :}
	| NEG expr:e
		{: RESULT = new neg(curr_lineno(), e); :}
	| expr:e1 LT expr:e2
		{: RESULT = new lt(curr_lineno(), e1, e2); :}
	| expr:e1 LE expr:e2
		{: RESULT = new leq(curr_lineno(), e1, e2); :}
	| expr:e1 EQ expr:e2
		{: RESULT = new eq(curr_lineno(), e1, e2); :}
	| NOT expr:e
		{: RESULT = new comp(curr_lineno(), e); :}
	| LPAREN expr:e RPAREN
		{: RESULT = e; :}
	| OBJECTID:oid
		{: RESULT = new object(curr_lineno(), oid); :}
	| INT_CONST:i
		{: RESULT = new int_const(curr_lineno(), i); :}
	| STR_CONST:s
		{: RESULT = new string_const(curr_lineno(), s); :}
	| BOOL_CONST:b
		{: RESULT = new bool_const(curr_lineno(), b); :}
	;

disp
	::= expr:e DOT OBJECTID:oid LPAREN expr_list_comma:el RPAREN
		{: RESULT = new dispatch(curr_lineno(), e, oid, el); :}
	| OBJECTID:oid LPAREN expr_list_comma:el RPAREN
		{: RESULT = new dispatch(curr_lineno(),
			(new object(curr_lineno(),
			AbstractTable.idtable.addString("self"))), oid, el);
		:}
	;

let_expr
	::= OBJECTID:oid COLON TYPEID:tid let_assign:la IN expr:e
		{: RESULT = new let(curr_lineno(), oid, tid, la, e); :}
	| OBJECTID:oid COLON TYPEID:tid let_assign:la COMMA let_expr:le
		{: RESULT = new let(curr_lineno(), oid, tid, la, le); :}
	| error COMMA let_expr
	;

let_assign
	::= /* empty */
		{: RESULT = new no_expr(curr_lineno()); :}
	| ASSIGN expr:e
		{: RESULT = e; :}
	;
	
cases
	::= case:c
		{: RESULT = (new Cases(curr_lineno())).appendElement(c); :}
	| cases:cs case:c
		{: RESULT = cs.appendElement(c); :}
	;

case
	::= OBJECTID:oid COLON TYPEID:tid DARROW expr:e SEMI
		{: RESULT = new branch(curr_lineno(), oid, tid, e); :}
	;
